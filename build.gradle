plugins {
    id 'net.neoforged.gradleutils' version '3.0.0' apply false
    id 'com.diffplug.spotless' version '6.22.0' apply false
}

apply plugin: 'net.neoforged.gradleutils'

project.version = '1.21.4-dev'

// Print version, generally useful to know - also appears on CI
System.out.println("NeoForge version ${project.version}")

allprojects {
    version rootProject.version
    group 'net.neoforged'

    apply plugin: 'java'

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-Xlint:-dep-ann' << '-Xlint:-removal' << '-Xdiags:verbose'
    }

    java.toolchain.languageVersion.set(JavaLanguageVersion.of(project.java_version))
}

// Remove src/ sources from the root project. They are used in the neoforge subproject.
sourceSets {
    main {
        java {
            srcDirs = []
        }
        resources {
            srcDirs = []
        }
    }
}